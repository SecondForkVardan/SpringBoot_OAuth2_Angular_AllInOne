buildscript {
	ext {
		springBootVersion = '1.5.9.RELEASE'
	}
	repositories {
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("com.moowork.gradle:gradle-node-plugin:1.2.0")
		classpath("gradle.plugin.com.boxfuse.client:flyway-release:4.2.0")
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'com.moowork.node'
apply plugin: 'org.flywaydb.flyway'

group = 'com.company'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}


dependencies {
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-security')
	compile('org.springframework.security.oauth:spring-security-oauth2')


	runtime('mysql:mysql-connector-java')
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('org.springframework.security:spring-security-test')
}

flyway {
	url = 'jdbc:mysql://localhost:3306/oauth'
	user = 'root'
	password = 'root'
	schemas = ['oauth']
}

node {
	version = '8.1.4'
	npmVersion = '5.5.1'
	download = true
	workDir = file("${project.buildDir}/nodejs")
	npmWorkDir = file("${project.projectDir}/front")
	nodeModulesDir = file("${project.projectDir}/front")
}

task buildAngular(type: NpmTask){
	group 'build'
	args = ['run', 'build']
}

task copyAngularToStaticResources(type: Copy){
	group "build"
	from "${projectDir}/front/dist"
	into "${projectDir}/src/main/resources/static"
	include '*', '*/*', '*/**'
	dependsOn buildAngular
	outputs.upToDateWhen {false}
}

buildAngular.dependsOn npmInstall